use crate::{lexer::{Token, LexerError}, Value};
use stahl_errors::Location;
use stahl_util::{SharedPath, SharedString};

grammar(path: &Option<SharedPath>);

extern {
	type Location = usize;
	type Error = LexerError;

	enum Token {
    	"(" => Token::ParenOpen,
    	")" => Token::ParenClose,
		"_" => Token::Hole,
    	"|" => Token::Pipe,
    	Int => Token::Int(<isize>),
    	String => Token::String(<SharedString>),
    	Symbol => Token::Symbol(<SharedString>),
	}
}

pub Values = <Value*>;

pub Value: Value = {
	"(" <ListBody>,
	<l:@L> "_" <r:@R> => Value::Symbol(Location::new_span(path.clone(), l, r),
	                                   SharedString::from("_")),
	<l:@L> <n:Int> <r:@R> => Value::Int(Location::new_span(path.clone(), l, r), n),
	<l:@L> <s:String> <r:@R> => Value::String(Location::new_span(path.clone(), l, r), s),
	<l:@L> <s:Symbol> <r:@R> => Value::Symbol(Location::new_span(path.clone(), l, r), s),
};

ListBody: Value = {
	<l:@L> <h: Value> <t: ListBody> <r:@R> => Value::Cons(
		Location::new_span(path.clone(), l, r),
		Box::new(h),
		Box::new(t),
	),
	"|" <v: Value> ")" => v,
	<l:@L> ")" <r:@R> => Value::Nil(Location::new_span(path.clone(), l, r)),
};
